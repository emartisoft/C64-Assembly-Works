;=-=-=-=-=-==-=-=-=-=-==-=-=-=-=-==-=-=-=-=-==-=-=-=-=-=
; Multi Interrupt
;=-=-=-=-=-==-=-=-=-=-==-=-=-=-=-==-=-=-=-=-==-=-=-=-=-=
; Generated by CBM prg Studio

; 10 SYS (2064)

*=$0801

        byte    $0E, $08, $0A, $00, $9E, $20, $28, $32 
        byte    $30, $36, $34, $29, $00, $00, $00

*=$0810


        ; init music
        lda #$00
        jsr $1000

        ; clear screen
        ldx #$00
        lda #$20
        sta $0400,x
        sta $0500,x
        sta $0600,x
        sta $0700,x
        dex
        bne *-13

        ldx #$02
        ldy #$02
        jsr $e50c

        lda #<MESAJ
        ldy #>MESAJ
        jsr $ab1e

        ; disable interrupts
        sei
        ; turn off kernal and basic
        lda #$35
        sta $01
        ; turn off cia interrupts
        lda #$7f
        sta $dc0d
        sta $dd0d
        ; enable raster irq
        lda #$01
        sta $d01a

        ; set text mode
        lda #$1b
        ldx #$08
        ldy #$14
        sta $d011
        stx $d016
        sty $d018

        ; init irq
        lda #<irq1
        ldx #>irq1
        sta $FFFE
        stx $FFFF

        ; create raster interrupt at line 0
        ldy #$00
        sty $d012

        ; clear interrupts
        lda $dc0d
        lda $dd0d
        cli

        jmp *

irq1
        ; saves A, X, Y
        pha
        txa
        pha
        tya
        pha

        lda #$0f
        sta $d020

        ; play music
        jsr $1003

        lda #<irq2
        ldx #>irq2
        sta $FFFE
        stx $FFFF

        ldy #$40
        sty $d012

        lsr $d019

        ; restores A, X, Y
        pla
        tay
        pla
        tax
        pla
        rti

irq2
        ; saves A, X, Y
        pha
        txa
        pha
        tya
        pha

        lda #$08
        sta $d020

        lda #<irq1
        ldx #>irq1
        sta $FFFE
        stx $FFFF

        ldy #$45
        sty $d012

        lsr $d019
        
        ; restores A, X, Y
        pla
        tay
        pla
        tax
        pla
        rti

MESAJ   TEXT "nothing else matters - sid by emarti"
        BYTE $00

*=$1000
        INCBIN "nothingelsematters.bin"
